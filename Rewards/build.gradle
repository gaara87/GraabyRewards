buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.neenbedankt.android-apt'


repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        releaseSigning {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 23
    buildToolsVersion '23.0.0'
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        multiDexEnabled true
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            // disable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseSigning
            // enable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    productFlavors {
        prod {
            applicationId "graaby.app.wallet"
            versionName "Zeta1 Scorpii"
            versionCode 37
        }
        dev {
            applicationId "graaby.app.wallet.dev"
            versionName "Zeta1 Scorpii-dev"
            versionCode 37
        }
    }
    applicationVariants.all { variant ->
        variant.buildConfigField "String", "ACCOUNT_AUTHENTICATOR", "\"" + variant.applicationId + ".account.auth\""
        variant.resValue "string", "account_authenticator", "\"" + variant.applicationId + ".account.auth\""

        variant.buildConfigField "String", "SEARCH_RECENT_PROVIDER", "\"" + variant.applicationId + ".provider\""
        variant.resValue "string", "search_recent_provider", "\"" + variant.applicationId + ".provider\""

        variant.outputs.each { output ->
            def file = output.outputFile

            String parent = file.parent
            if (project.hasProperty('OUTPUT_DIR') && new File((String) OUTPUT_DIR).exists())
                parent = OUTPUT_DIR

            output.outputFile = new File(
                    parent,
                    (String) file.name.replace(
                            ".apk",

                            // alter this string to change output file name
                            "-" + variant.versionName + "-build" + variant.versionCode + "-.apk"
                    )
            )
        }
    }
    enforceUniquePackageName = false
    //remove the above line in gradle version above 1 if the error has been fixed

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
apply plugin: 'com.neenbedankt.android-apt'

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0].processResources.manifestFile
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.intellij:annotations:12.0'
    compile 'com.android.support:support-annotations:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.google.android.gms:play-services-analytics:7.8.0'
    compile 'com.google.android.gms:play-services-gcm:7.8.0'
    compile 'com.google.android.gms:play-services-identity:7.8.0'
    compile 'com.google.android.gms:play-services-maps:7.8.0'
    compile 'com.google.android.gms:play-services-location:7.8.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.mobsandgeeks:android-saripaar:2.0.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'io.realm:realm-android:0.82.1'
    compile 'de.greenrobot:eventbus:3.0.0-beta1'
    provided 'de.greenrobot:eventbus-annotation-processor:3.0.0-beta1'
    compile 'io.reactivex:rxandroid:1.0.1'
    apt 'com.bluelinelabs:logansquare-compiler:1.1.0'
    compile 'com.bluelinelabs:logansquare:1.1.0'
    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.github.bumptech.glide:okhttp-integration:1.3.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true
    }
    compile 'com.github.anrwatchdog:anrwatchdog:1.1.+'
}
buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        releaseSigning {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 22
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseSigning
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    productFlavors {
        prod {
            applicationId "graaby.app.wallet"
            versionName "1"
            versionCode 32
        }
        dev {
            applicationId "graaby.app.wallet.dev"
            versionName "2-dev"
            versionCode 34
        }
    }
    applicationVariants.all { variant ->
        variant.buildConfigField "String", "ACCOUNT_AUTHENTICATOR", "\"" + variant.applicationId + ".account.auth\""
        variant.resValue "string", "account_authenticator", "\"" + variant.applicationId + ".account.auth\""

        variant.buildConfigField "String", "SEARCH_RECENT_PROVIDER", "\"" + variant.applicationId + ".provider\""
        variant.resValue "string", "search_recent_provider", "\"" + variant.applicationId + ".provider\""

        variant.outputs.each { output ->
            def file = output.outputFile

            String parent = file.parent
            if (project.hasProperty('OUTPUT_DIR') && new File((String) OUTPUT_DIR).exists())
                parent = OUTPUT_DIR

            output.outputFile = new File(
                    parent,
                    (String) file.name.replace(
                            ".apk",

                            // alter this string to change output file name
                            "-" + variant.versionName + "-build" + variant.versionCode + "-.apk"
                    )
            )
        }
    }
    enforceUniquePackageName = false
    //remove the above line in gradle version above 1 if the error has been fixed

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
apply plugin: 'com.neenbedankt.android-apt'

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0].processResources.manifestFile
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-annotations:20.0.0'
    compile 'com.intellij:annotations:12.0'

    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:21.0.3'

    compile 'com.google.android.gms:play-services-analytics:7.3.0'
    compile 'com.google.android.gms:play-services-gcm:7.3.0'
    compile 'com.google.android.gms:play-services-identity:7.3.0'
    compile 'com.google.android.gms:play-services-maps:7.3.0'
    compile 'com.google.android.gms:play-services-location:7.3.0'
    compile 'com.google.maps.android:android-maps-utils:0.3.2'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'

    compile 'com.mobsandgeeks:android-saripaar:2.0-SNAPSHOT'

    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'de.hdodenhof:circleimageview:1.2.1'

    compile 'io.realm:realm-android:0.80.1'

    compile 'de.greenrobot:eventbus:2.4.0'

    compile 'io.reactivex:rxandroid:0.24.0'

    compile 'com.github.navasmdc:MaterialDesign:1.+@aar'
    compile 'com.rengwuxian.materialedittext:library:2.0.3'
    compile 'com.melnykov:floatingactionbutton:1.3.0'

    apt 'com.bluelinelabs:logansquare-compiler:1.1.0'
    compile 'com.bluelinelabs:logansquare:1.1.0'

    compile 'com.github.bumptech.glide:glide:3.5.2'
    compile 'com.github.bumptech.glide:okhttp-integration:1.2.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') {
        transitive = true;
    }
}